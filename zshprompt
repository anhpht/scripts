function precmd
{
    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))
    PR_FILLBAR=""
    PR_PWDLEN=""

    local promptsize=${#${(%):---(%n@%m)-----}}
    local pwdsize=${#${(%):-%~}}

    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
        ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
        PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi
}

setprompt ()
{
    setopt prompt_subst

    # See if we can use colors.
    autoload colors zsh/terminfo
    if [[ "$terminfo[colors]" -ge 8 ]]; then
        colors
    fi
        for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
            #eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
            #eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
            eval PR_$color='%{$fg[${(L)color}]%}'
            eval PR_LIGHT_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
            (( count = $count + 1 ))
        done
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"

    # See if we can use extended characters to look nicer.
    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
    PR_SET_CHARSET="%{$terminfo[enacs]%}"
    PR_SHIFT_IN="%{$terminfo[smacs]%}"
    PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_YELLOW$PR_SHIFT_IN$PR_ULCORNER$PR_RED$PR_HBAR$PR_SHIFT_OUT(\
$PR_YELLOW%(!.%SROOT%s.%n)$PR_GREEN@%m: $PR_MAGENTA%$PR_PWDLEN<...<%~%<<\
$PR_RED)\

$PR_YELLOW$PR_SHIFT_IN$PR_LLCORNER$PR_RED$PR_HBAR$PR_SHIFT_OUT(\
%(?..$PR_LIGHT_RED%?$PR_RED:)\
$PR_YELLOW%T\
$PR_LIGHT_RED:%(!.$PR_RED.$PR_WHITE)%#$PR_RED)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_NO_COLOUR'
}

